plugins {
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.springframework.boot' version "2.3.0.RELEASE" apply false
	id 'java'
}

ext {
	springBootVersion = '2.3.0.RELEASE'
	springCloudVersion = 'Hoxton.SR4'
	springDocVersion = '1.4.2-SNAPSHOT'
}

allprojects {
	group = 'org.springdoc'
	version = springDocVersion
}

repositories {
	mavenLocal()
	mavenCentral()
}


subprojects {

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'

	springBoot {
		buildInfo()
	}

	bootJar {
		archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
	}

	configurations {
		testCompileOnly.extendsFrom compileOnly
		developmentOnly
		runtimeClasspath {
			extendsFrom developmentOnly
		}
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
		testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	}

	dependencyManagement {
		imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
		imports { mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}") }
		dependencies {
			dependency "org.springdoc:springdoc-openapi-webmvc-core:${springDocVersion}"
			dependency "org.springdoc:springdoc-openapi-webflux-ui:${springDocVersion}"
		}
	}

	test {
		useJUnitPlatform()
	}

	sourceCompatibility = '1.8'

	java {
		withSourcesJar()
		withJavadocJar()
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}
		}
		repositories {
			maven {
				if (project.version.endsWith('-SNAPSHOT')) {
					url "https://oss.sonatype.org/content/repositories/snapshots"
				} else {
					url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				}
				credentials {
					username System.getenv("OSSRH_USER")
					password System.getenv("OSSRH_PASS")
				}
			}
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}
}
